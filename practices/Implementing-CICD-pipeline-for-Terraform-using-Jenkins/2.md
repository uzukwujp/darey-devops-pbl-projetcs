## Managing Cloud Infrastructure Using Terraform

Infrastructure as Code (IaC) is the managing and provisioning of infrastructure through code instead of through manual processes. 

IaC helps us define the specifications of our infrastructure in files called configuration files. 

This allows us to easily edit and distribute them within our organization or publicly. 

Terraform is the leading technology in this family of tools. There are other options such as **Pulumi**, **CloudFormation**, **Azure Bicep** etc.

In this module we will create a simple configuration file to provison an EC2 instance as well as a key-pair in AWS.

While carrying out this practice you will familiarize your self with basic concepts of terraform.

**Step 1:** Create  Folder in your local machine an example is shown below:

```
    mkdir jenkins_lab
```

**Step 2:** Change into the folder, create the following files: *main.tf*, *provider.tf*, *variables.tf*. Paste the content for each of the files below:

main.tf:
```
# generating a key material
resource "tls_private_key" "jenkins_lab" {
  algorithm = "RSA"
  rsa_bits  = 4096
}

# creating an aws key pair
resource "aws_key_pair" "jenkins_key" {
  key_name   = var.key_name
  public_key = tls_private_key.jenkins_lab.public_key_openssh
}


# stores the private key of the aws key pair to computers filesysyem.
resource "local_file" "jenkins_file" {
  content  = tls_private_key.jenkins_lab.private_key_pem
  filename = "${path.module}/jenkins.pem"
}


# get the AMI from AWS
data "aws_ami" "ubuntu" {
  most_recent = true

  filter {
    name   = "name"
    values = ["ubuntu/images/hvm-ssd/ubuntu-focal-20.04-amd64-server-*"]
  }

  filter {
    name   = "virtualization-type"
    values = ["hvm"]
  }

  owners = ["099720109477"] # Canonical
}

# Creates an EC2 instance.
resource "aws_instance" "ubuntu_instance" {
  ami           = data.aws_ami.ubuntu.id
  instance_type = "t2.micro"
  key_name      = aws_key_pair.jenkins_key.key_name
}

```

variables.tf:

```
    variable "region" {
    type = string
    description = "AWS region"

}

variable "key_name" {
    type = string
    description = "key pair name"
}
```

provider.tf:

```
    provider "aws" {
    region = var.region
    }
```

**Step 3:** Next step is to create the ec2 instance as well as the key pair. You can do so with the commands below:

- Download the required terraform plugins:

```
    terraform init
```

- Plan the infrasturcture:

```
    terraform plan
```

- Create the infrastructure:
```
    terraform apply
```

